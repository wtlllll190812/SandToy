#include "Utils.hlsl"
#pragma kernel CSMain

RWTexture2D<float4> Result;
int seed;

bool checkKind(uint2 id,uint kind)
{
    return value2Kind(Result[id.xy])==kind;
}

void exchange(uint2 id,uint2 nbrId)
{
    float temp=Result[id.xy].x;
    Result[id.xy]=float4(Result[nbrId].x,Result[id.xy].yzw);
    Result[nbrId]=float4(temp.x,Result[nbrId].yzw);
}

void setKind(uint2 id,uint kind)
{
    float4 ori=Result[id.xy];
    Result[id.xy]=float4(kind2Value(kind),ori.yzw);
}

void updateSand(uint3 id)
{
    //重力
    if(isEmpty(Result[down(id)]))
    {
        exchange(id.xy,down(id));
    }
    else if(isLiquid(Result[down(id)]))
    {
        exchange(id.xy,down(id));
    }
}

void updateGas(uint3 id)
{
    //随机游动
    uint2 vec=randDir(id,seed);
    if(canPass(Result[(id.xy+vec)])&&!checkKind(id.xy+vec*2,ELE_GAS))
    {
        exchange(id.xy,id.xy+vec);
    }
}

void updateWater(uint3 id)
{
    float4 ori=Result[id.xy];
    float4 nbr=Result[down(id)];
    
    //重力
    if(isAir(nbr))
    {
        exchange(id.xy,down(id));
        return;
    }
    if(isAir(Result[left(id)])&&isAir(Result[right(id)]))
    {
        if(rand(id,seed)>0.5f&&!checkKind(left_2(id),ELE_WATER))
        {
            exchange(id.xy,left(id));
        }
        else if(!checkKind(right_2(id),ELE_WATER))
        {
            exchange(id.xy,right(id));
        }
        
        return;
    }
    
    //扩散
    nbr=Result[left(id)];
    if(isAir(nbr)&&!checkKind(left_2(id),ELE_WATER))
    {
        exchange(id.xy,left(id));
        return;
    }
    nbr=Result[right(id)];
    if(isAir(nbr)&&!checkKind(right_2(id),ELE_WATER))
    {
        exchange(id.xy,right(id));
        return;
    }
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint kind=value2Kind(Result[id.xy]);
    switch (kind)
    {
    case ELE_SAND:  updateSand(id);  break;
    case ELE_GAS:   updateGas(id);   break;
    case ELE_WATER: updateWater(id); break;
    default: break;
    }
}
