#include "../Utils.hlsl"
#pragma kernel CSMain

RWTexture2D<float4> Result;
RWTexture2D<float4> Environment;
int seed;

/**
 * \brief 获取燃点
 */
uint flamePoint(uint kind)
{
    switch (kind)
    {
    case OIL: return 25;
    case PLANT: return 26;
    case FLOWER: return 26;
    case WOOD: return 27;
    case SEED: return 28;
    case ROOT: return 28;
    default: return 32;
    }
}

/**
 * \brief 获取燃烧时间
 */
uint flameTime(uint kind)
{
    switch (kind)
    {
    case OIL: return 30;
    case WOOD: return 20;
    case PLANT: return 15;
    case ROOT: return 15;
    case FLOWER: return 3;
    case SEED: return 3;
    default: return 32;
    }
}

/**
 * \brief 更新火焰
 */
void updateFire(uint3 id)
{
    setTemperature(id.xy,int2float(31));
    uint2 vec=randDir(id,seed,DIR_UP);
    uint2 nbrId=id.xy+vec;
    uint number=Result[id.xy].y*ELEMENT_NUMBER;

    if(number==0||checkKind(id.xy,WATER))
    {
        setKind(id.xy,EMPTY);
        setY(id.xy,0);
    }
    
    if(canPass(Result[nbrId]))
    {
        move(id.xy,nbrId);
        setY(nbrId,rand(id,seed)>0.2f?number-1:number);
        setY(id.xy,0);
    }
}

/**
 * \brief 更新火源
 */
void updateFireSource(uint3 id)
{
    uint kind1=float2int(Result[left(id)].x);
    uint kind2=float2int(Result[right(id)].x);
    uint kind3=float2int(Result[down(id)].x);
    uint kind4=float2int(Result[up(id)].x);

    bool nearWater=kind1==WATER||kind2==WATER||kind3==WATER||kind4==WATER;

    if(nearWater)
    {
        setKind(id.xy,SAND);
    }
}

/**
 * \brief 更新可燃物
 */
void updateCombustible(uint3 id,uint kind)
{
    uint kind1=float2int(Result[left(id)].x);
    uint kind2=float2int(Result[right(id)].x);
    uint kind3=float2int(Result[down(id)].x);
    uint kind4=float2int(Result[up(id)].x);

    bool nearFire=kind1==FIRE||kind2==FIRE||kind3==FIRE||kind4==FIRE;
    bool nearFireSource=kind1==FIRE_SOURCE||kind2==FIRE_SOURCE||kind3==FIRE_SOURCE||kind4==FIRE_SOURCE
                        ||kind1==F_FIRE_SOURCE||kind2==F_FIRE_SOURCE||kind3==F_FIRE_SOURCE||kind4==F_FIRE_SOURCE;
    bool nearAir=kind1==EMPTY||kind2==EMPTY||kind3==EMPTY||kind4==EMPTY;
    uint temp=float2int(Environment[id.xy].x);
    
    if(nearAir&&(nearFireSource||nearFire||temp>=flamePoint(kind)))
    {
        setKind(id.xy,kind==OIL?F_FIRE_SOURCE:FIRE_SOURCE);
        setY(id.xy,flameTime(kind));
    }
}

/**
 * \brief 生成火焰（y表示剩余火星数量）
 */
void spawnFire(uint3 id,bool isLiHuo)
{
    setTemperature(id.xy,int2float(32));
    uint2 nbrId=id.xy+randDir(id,seed,DIR_UP);
    float4 nbr=Result[nbrId];
    uint number=float2int(Result[id.xy].y);
    if(isEmpty(nbr))
    {
        setKind(nbrId,FIRE);
        setY(nbrId,40);
        if(!isLiHuo)setY(id.xy,number-1);
        if(number<=1)
        {
            setKind(id.xy,FIRE);
        }
    }
    else if(rand(id,seed)>0.5f)
    {
        if(!isLiHuo)setY(id.xy,number-1);
        if(number<=1)
        {
            setKind(id.xy,FIRE);
        }
    }
}

/**
 * \brief 判断是否可燃
 * \param kind 种类
 */
bool ignitable( uint kind)
{
    return kind==WOOD
            ||kind==SEED
            ||kind==PLANT
            ||kind==FLOWER
            ||kind==OIL
            ||kind==ROOT;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint kind =float2int(Result[id.xy].x);

    if(checkKind(id.xy,FIRE_SOURCE)||checkKind(id.xy,F_FIRE_SOURCE))
    {
        updateFireSource(id);
        spawnFire(id,false);
    }
    else if(checkKind(id.xy,LIHUO))
    {
        spawnFire(id,true);
    }
    else if(checkKind(id.xy,FIRE))
        updateFire(id);
    else if(ignitable(kind))
        updateCombustible(id,kind);
}

void setTemperature(uint2 id,float value)
{
    float4 ori=Environment[id.xy];
    Environment[id.xy]=float4(value,ori.yzw);
}

bool checkKind(uint2 id,uint kind)
{
    return float2int(Result[id.xy].x)==kind;
}

void move(uint2 id,uint2 nbrId)
{
    float4 temp=Result[id.xy];
    Result[id.xy]=Result[nbrId];
    Result[nbrId]=temp;
}

void setKind(uint2 id,uint kind)
{
    float4 ori=Result[id.xy];
    Result[id.xy]=float4(int2float(kind),ori.yzw);
}

void setY(uint2 id,uint value)
{
    float4 ori=Result[id.xy];
    Result[id.xy]=float4(ori.x,int2float(value),ori.zw);
}

void setZ(uint2 id,uint value)
{
    float4 ori=Result[id.xy];
    Result[id.xy]=float4(ori.x,ori.y,int2float(value),ori.w);
}

void setW(uint2 id,uint value)
{
    float4 ori=Result[id.xy];
    Result[id.xy]=float4(ori.x,ori.y,ori.z,int2float(value));
}